// identification
let NAME = "avr.ALInstructions"
let VERSION = "1.0"
let ARCH = "avr"
let BUILDER = "atmel"
let LICENSE = "GPL"
let COPYRIGHT = "University of Toulouse"
let YEAR = "2019"
let URL = ""

include "config.nmp"


////// Arithmetic and Logic Instructions //////

op ADD(d: rindex, r: rindex)
	syntax = format("ADD R%d, R%d", d, r)
	image = format("0000 11%1b%1b %4b %4b", r<4..4>, d<4..4>, d<3..0>, r<3..0>)
	action = {
		let R_ = R[d] + R[r];
		SREG_C = (R[d]<7..7> && R[r]<7..7>) || (R[r]<7..7> && !R_<7..7>) || (!R_<7..7> && R[d]<7..7>);
		SREG_Z = R_ == 0;
		SREG_V = (R[d]<7..7> && R[r]<7..7> && !R_<7..7>) || (!R[d]<7..7> && !R[r]<7..7> && R_<7..7>);
		SREG_S = SREG_N ^ SREG_V;
		SREG_H = (R[d]<3..3> && R[r]<3..3>) || (R[r]<3..3> && !R_<3..3>) || (!R_<3..3> && R[d]<3..3>);
		R[d] = R_;
	}

op ADC(d: rindex, r: rindex)
	syntax = format("ADC R%d, R%d", d, r)
	image = format("0001 11%1b%1b %4b %4b", r<4..4>, d<4..4>, d<3..0>, r<3..0>)
	action = {
		let R_ = R[d] + R[r] + SREG_C;
		SREG_C = (R[d]<7..7> && R[r]<7..7>) || (R[r]<7..7> && !R_<7..7>) || (!R_<7..7> && R[d]<7..7>);
		SREG_Z = R_ == 0;
		SREG_N = R_<7..7>;
		SREG_V = (R[d]<7..7> && R[r]<7..7> && !R_<7..7>) || (!R[d]<7..7> && !R[r]<7..7> && R_<7..7>);
		SREG_S = SREG_N ^ SREG_V;
		SREG_H = (R[d]<3..3> && R[r]<3..3>) || (R[r]<3..3> && !R_<3..3>) || (!R_<3..3> && R[d]<3..3>);
		R[d] = R_;
	}

op ADIW(d: card(2), k: card(6))
	dl = 24 + d*2
	dh = dl + 1
	syntax = format("ADIW R%d:R%d, %d", dh, dl, k)
	image = format("1001 0110 %2b%2b %4b", k<5..4>, d, k<3..0>)
	action = {
		let R_ = (R[dh]::R[dl]) + k;
		SREG_V = !R[dh]<7..7> && R_<15..15>;
		SREG_N = R_<15..15>;
		SREG_Z = R_ == 0;
		SREG_C = !R_<15..15> && R[dh]<7..7>;
		SREG_S = SREG_N ^ SREG_V;
		R[dh]::R[dl] = R_;
	}

op SBIW(d: card(2), k: card(6))
	dl = 24 + d*2
	dh = dl + 1
	syntax = format("SBIW R%d:R%d, %d", dh, dl, k)
	image = format("1001 0111 %2b%2b %4b", k<5..4>, d, k<3..0>)
	action = {
		let R_ = (R[dh]::R[dl]) - k;
		SREG_V = R[dh]<7..7> && !R_<15..15>;
		SREG_N = R_<15..15>;
		SREG_Z = R_ == 0;
		SREG_C = R_<15..15> && !R[dh]<7..7>;
		SREG_S = SREG_N ^ SREG_V;
		R[dh]::R[dl] = R_;
	}
